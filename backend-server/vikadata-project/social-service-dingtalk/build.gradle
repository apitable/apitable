import java.nio.charset.StandardCharsets
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
  id 'java'
  id 'org.springframework.boot'
}

dependencies {
  implementation project(':vikadata-core')
  implementation project(':vikadata-define')
  implementation project(':vikadata-starters:vikadata-starter-swagger')
  implementation project(':vikadata-starters:vikadata-starter-grpc-model')
  implementation project(':vikadata-starters:vikadata-starter-social-dingtalk')
  
  implementation rootProject.ext.dependencies['hutool']
  implementation rootProject.ext.dependencies['p6spy-starter']
  implementation rootProject.ext.dependencies['okhttp3']
  implementation rootProject.ext.dependencies['swagger2']
  implementation rootProject.ext.dependencies['spring-retry']
  implementation rootProject.ext.dependencies['spring-web-starter']
  implementation('org.springframework.boot:spring-boot-starter-data-redis') {
    exclude group: 'io.lettuce', module: 'lettuce-core'
  }
  implementation('org.apache.commons:commons-pool2')
  implementation rootProject.ext.dependencies['jedis']
  implementation rootProject.ext.dependencies['grpc-server-spring-boot']
  implementation rootProject.ext.dependencies['spring-actuator-starter']
  implementation rootProject.ext.dependencies['spring-integration-redis']
  implementation rootProject.ext.dependencies['spring-validation-starter']
  implementation rootProject.ext.dependencies['spring-cloud-starter-sleuth']
  
  annotationProcessor rootProject.ext.dependencies['configuration-processor']
  
  implementation rootProject.ext.dependencies['mybatis-plus-starter']
  runtimeOnly rootProject.ext.dependencies['mysql']
  
  annotationProcessor rootProject.ext.dependencies['lombok']
  compileOnly rootProject.ext.dependencies['lombok']
  
  
  testCompileOnly rootProject.ext.dependencies["lombok"]
  testAnnotationProcessor rootProject.ext.dependencies["lombok"]
  testImplementation rootProject.ext.dependencies['spring-test-starter']
}

jar {
  enabled = false
}
processResources {
  filesMatching("**/*.yml") {
    if (it.name != 'application.yml') {
      it.exclude()
    }
  }
}

compileJava.dependsOn processResources
bootJar.dependsOn processResources

// Compile Java Setting
tasks.withType(JavaCompile) {
  options.encoding = StandardCharsets.UTF_8
  options.warnings = false
  options.compilerArgs += '-Xlint:-deprecation'
}

bootJar {
  mainClass.set('com.vikadata.social.service.dingtalk.SocialDingTalkApplication')
  manifest {
    attributes 'Start-Class': 'com.vikadata.social.service.dingtalk.SocialDingTalkApplication'
    attributes 'Implementation-Version': archiveVersion
    attributes 'Built-Gradle': gradle.gradleVersion
    attributes 'Build-OS': System.getProperty("os.name")
    attributes 'Built-By': System.getProperty("user.name")
    attributes 'Build-Jdk': System.getProperty("java.version")
    attributes 'Build-Timestamp': LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss"))
  }
}

// SpringBoot Plugin On Gradle
springBoot {
  mainClass.set('com.vikadata.social.service.dingtalk.SocialDingTalkApplication')
  // build-info.properties in resources/main/META-INF directory
  buildInfo {
    properties {
      artifact = 'Socail Api Server'
      version = rootProject.version
      group = rootProject.group
      name = 'Socail Api Server'
    }
  }
}